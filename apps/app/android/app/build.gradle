apply plugin: "com.android.application"
apply plugin: "org.jetbrains.kotlin.android"
apply plugin: "com.facebook.react"

/**
 * This is the configuration block to customize your React Native Android app.
 * By default you don't need to apply any configuration, just uncomment the lines you need.
 */
// monorepo: node_modules may be hoisted to repo root or live in apps/app/node_modules
def findNodeModules() {
    def local = new File(projectDir, "../../node_modules/react-native")
    if (local.exists()) return file("../../node_modules")
    def root = new File(projectDir, "../../../../node_modules/react-native")
    if (root.exists()) return file("../../../../node_modules")
    throw new GradleException("Could not find react-native in node_modules")
}
def nm = findNodeModules()

react {
    /* Folders */
    root = file("../../")
    reactNativeDir = new File(nm, "react-native")
    codegenDir = new File(nm, "@react-native/codegen")
    cliFile = new File(nm, "react-native/cli.js")

    /* Variants */
    //   The list of variants to that are debuggable. For those we're going to
    //   skip the bundling of the JS bundle and the assets. By default is just 'debug'.
    //   If you add flavors like lite, prod, etc. you'll have to list your debuggableVariants.
    // debuggableVariants = ["liteDebug", "prodDebug"]

    // in CI there is no metro server, so bundle JS into the debug APK
    if (project.hasProperty("bundleInDebug")) {
        debuggableVariants = []
    }

    /* Bundling */
    //   A list containing the node command and its flags. Default is just 'node'.
    // nodeExecutableAndArgs = ["node"]
    //
    //   The command to run when bundling. By default is 'bundle'
    // bundleCommand = "ram-bundle"
    //
    //   The path to the CLI configuration file. Default is empty.
    // bundleConfig = file(../rn-cli.config.js)
    //
    //   The name of the generated asset file containing your JS bundle
    // bundleAssetName = "MyApplication.android.bundle"
    //
    //   The entry file for bundle generation. Default is 'index.android.js' or 'index.js'
    // entryFile = file("../js/MyApplication.android.js")
    //
    //   A list of extra flags to pass to the 'bundle' commands.
    //   See https://github.com/react-native-community/cli/blob/main/docs/commands.md#bundle
    // extraPackagerArgs = []

    /* Hermes Commands */
    hermesCommand = new File(nm, "react-native/sdks/hermesc/%OS-BIN%/hermesc").absolutePath

    /* Autolinking */
    autolinkLibrariesWithApp()
}

/**
 * Set this to true to Run Proguard on Release builds to minify the Java bytecode.
 */
def enableProguardInReleaseBuilds = false

/**
 * The preferred build flavor of JavaScriptCore (JSC)
 *
 * For example, to use the international variant, you can use:
 * `def jscFlavor = 'org.webkit:android-jsc-intl:+'`
 *
 * The international variant includes ICU i18n library and necessary data
 * allowing to use e.g. `Date.toLocaleString` and `String.localeCompare` that
 * give correct results when using with locales other than en-US. Note that
 * this variant is about 6MiB larger per architecture than default.
 */
def jscFlavor = 'org.webkit:android-jsc:+'

android {
    ndkVersion rootProject.ext.ndkVersion
    buildToolsVersion rootProject.ext.buildToolsVersion
    compileSdk rootProject.ext.compileSdkVersion

    namespace "com.sharego"
    defaultConfig {
        applicationId "com.sharego"
        minSdkVersion rootProject.ext.minSdkVersion
        targetSdkVersion rootProject.ext.targetSdkVersion
        versionCode 4
        versionName "0.4.0"
    }
    signingConfigs {
        debug {
            storeFile file('debug.keystore')
            storePassword 'android'
            keyAlias 'androiddebugkey'
            keyPassword 'android'
        }
        release {
            // reads from gradle.properties or env vars
            // generate keystore: keytool -genkeypair -v -storetype PKCS12 \
            //   -keystore sharego-release.keystore -alias sharego \
            //   -keyalg RSA -keysize 2048 -validity 10000
            def ksFile = System.getenv("SHAREGO_KEYSTORE_FILE") ?: project.findProperty("SHAREGO_KEYSTORE_FILE")
            if (ksFile) {
                storeFile file(ksFile)
                storePassword System.getenv("SHAREGO_KEYSTORE_PASSWORD") ?: project.findProperty("SHAREGO_KEYSTORE_PASSWORD") ?: ""
                keyAlias System.getenv("SHAREGO_KEY_ALIAS") ?: project.findProperty("SHAREGO_KEY_ALIAS") ?: "sharego"
                keyPassword System.getenv("SHAREGO_KEY_PASSWORD") ?: project.findProperty("SHAREGO_KEY_PASSWORD") ?: ""
            }
        }
    }
    buildTypes {
        debug {
            signingConfig signingConfigs.debug
        }
        release {
            // use release keystore if configured, otherwise fall back to debug
            signingConfig signingConfigs.release.storeFile ? signingConfigs.release : signingConfigs.debug
            minifyEnabled enableProguardInReleaseBuilds
            proguardFiles getDefaultProguardFile("proguard-android.txt"), "proguard-rules.pro"
        }
    }
    // prevent duplicate native lib conflicts when multiple RN modules bundle libc++_shared.so
    packaging {
        jniLibs {
            pickFirst '**/*.so'
        }
    }
}

dependencies {
    // The version of react-native is set by the React Native Gradle Plugin
    implementation("com.facebook.react:react-android")

    if (hermesEnabled.toBoolean()) {
        implementation("com.facebook.react:hermes-android")
    } else {
        implementation jscFlavor
    }
}
